plugins {
    id 'java-library'
    id "com.github.ben-manes.versions" version "0.51.0"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation libs.junit.jupiter
    testImplementation libs.hamcrest
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

testing {
    suites {
        test {
            useJUnitJupiter(libs.versions.junit.jupiter)
            targets {
                all {
                    testTask.configure {
                        systemProperty 'java.util.logging.config.file', 'src/test/resources/logging.properties'
                        systemProperty 'jextract.trace.downcalls', 'true'
                        environment "LD_LIBRARY_PATH", "${project.buildDir}/lua-libs/"
                        environment "DYLD_LIBRARY_PATH", "${project.buildDir}/lua-libs/"
                        jvmArgs '--enable-native-access=ALL-UNNAMED'
                    }
                }
            }
        }
    }
}

OperatingSystem currentOs = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem
task downloadJextract(type: DownloadTask) {
    def getOs = {
        if(currentOs.isMacOsX()) {
            return "macos"
        }
        if(currentOs.isLinux()) {
            return "linux"
        }
        throw new IllegalStateException("Unsupported operating system: ${currentOs}")
    }
    def getArch = {
        def arch = System.getProperty("os.arch")
        switch(arch) {
            case "amd64":
                return "x64"
            default:
                throw new IllegalStateException("Unsupported architecture: ${arch}")
        }
    }
    sourceUrl = "https://download.java.net/java/early_access/jextract/22/5/openjdk-22-jextract+5-33_${getOs()}-${getArch()}_bin.tar.gz"
    target = file("${project.buildDir}/jextract.tar.gz")
}

task unpackJextract(type: Copy, dependsOn: [tasks.downloadJextract]) {
    from tarTree(resources.gzip(tasks.downloadJextract.outputs.files[0]))
    into file("${project.buildDir}/jextract/")
}

task buildLua(type: Exec) {
    def getMakeGoal = {
        if(currentOs.isLinux()) {
            return "so"
        }
        if(currentOs.isMacOsX()) {
            return "dylib"
        }
        throw new IllegalStateException("Unsupported operating system: ${currentOs}")
    }
    def luaSrc = "${projectDir}/lua/src"
    def installDir = "${project.buildDir}/lua-libs"
    workingDir luaSrc
    commandLine 'make', getMakeGoal()
    inputs.dir(luaSrc)
    outputs.dir(installDir)
    doLast {
        copy {
            from luaSrc
            into installDir
            include "liblua.*"
        }
    }
}

tasks.test.dependsOn(tasks.buildLua)
tasks.build.dependsOn(tasks.buildLua)

task cleanLua(type: Exec) {
    workingDir "${projectDir}/lua/src"
    commandLine 'make', 'clean'
}

clean.dependsOn(tasks.cleanLua)

task generateNativeInterface(type: Exec, dependsOn: [tasks.unpackJextract]) {
    def jextractBinary = "${tasks.unpackJextract.outputs.files[0]}/jextract-22/bin/jextract"
    def includeDir = "${project.rootDir}/lua/src"
    def generatedSrc = "${project.buildDir}/generated/sources/jextract/"
    commandLine jextractBinary,
                '--include-dir', includeDir,
                '--output', generatedSrc,
                '--target-package', 'org.itsallcode.jlua.ffi',
                '--library', 'lua',
                '--header-class-name', 'Lua',
                "$includeDir/all_lua.h"
    inputs.dir(includeDir)
    outputs.dir(generatedSrc)
    doLast {

    }
}

compileJava {
    options.compilerArgs += ['--enable-preview', '-Xlint:all,-preview,-restricted', '-Werror']
}

sourceSets {
    main {
        java {
            srcDir(generateNativeInterface)
        }
    }
}
