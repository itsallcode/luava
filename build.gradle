plugins {
    id 'java-library'
    id "com.github.ben-manes.versions" version "0.51.0"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation libs.junit.jupiter
    testImplementation libs.hamcrest
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

testing {
    suites {
        test {
            useJUnitJupiter(libs.versions.junit.jupiter)
            targets {
                all {
                    testTask.configure {
                        systemProperty 'java.util.logging.config.file', 'src/test/resources/logging.properties'
                        systemProperty 'jextract.trace.downcalls', 'true'
                        environment "LD_LIBRARY_PATH", "${project.buildDir}/lua-install/lib/"
                    }
                }
            }
        }
    }
}


task buildLua(type: Exec) {
    def luaSrc = "${projectDir}/lua"
    def installDir = "${project.buildDir}/lua-install"
    workingDir luaSrc
    commandLine 'make', 'all', 'install', "INSTALL_TOP=$installDir"
    inputs.dir(luaSrc)
    outputs.dir(installDir)
}

task generateNativeInterface(type: Exec, dependsOn: [tasks.buildLua]) {
    def installDir = tasks.buildLua.outputs.files[0]
    def generatedSrc = "${project.buildDir}/generated/sources/jextract/"
    commandLine 'jextract',
                '--include-dir', "$installDir/include",
                '--output', generatedSrc,
                '--target-package', 'org.itsallcode.jlua.ffi',
                '--library', 'lua',
                '--header-class-name', 'Lua',
                "$projectDir/lua/src/all_lua.h"
    inputs.dir(installDir)
    outputs.dir(generatedSrc)
}

compileJava {
    options.compilerArgs += ['--enable-preview', '-Xlint:all,-preview,-restricted', '-Werror']
}

sourceSets {
    main {
        java {
            srcDir(generateNativeInterface)
        }
    }
}
